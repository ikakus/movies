apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'features.gradle'

android {
    compileSdkVersion rootProject.ext.android.compileSdk
    defaultConfig {
        applicationId rootProject.ext.application.applicationId
        minSdkVersion rootProject.ext.android.minSdk
        targetSdkVersion rootProject.ext.android.targetSdk
        versionCode rootProject.ext.application.versionCode
        versionName rootProject.ext.application.versionName

        buildConfigField 'boolean', 'movieEnabled', "${features.movie}"
    }

    compileOptions {
        sourceCompatibility rootProject.ext.compiler.java
        targetCompatibility rootProject.ext.compiler.java
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            buildConfigField 'String', 'imdbApiUrl', "\"$imdbApiUrl\""
            buildConfigField 'String', 'defaultQuery', "\"warrior\""
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

// FAQ: It's for @Parcelize annotation and LayoutContainer interface.
androidExtensions {
    experimental = true
}

dependencies {
    implementation project(':core')
    implementation project(':data')
    implementation project(':domain')
    implementation project(':navigation')
    implementation project(':features:movies')
    implementation project(':features:movie')

    implementation rootProject.ext.dependencies.appcompatV7
    implementation rootProject.ext.dependencies.cicerone
    implementation rootProject.ext.dependencies.dagger
    implementation rootProject.ext.dependencies.daggerAndroid
    implementation rootProject.ext.dependencies.daggerAndroidSupport
    implementation rootProject.ext.dependencies.kotlinStdlib
    implementation rootProject.ext.dependencies.retrofit
    implementation rootProject.ext.dependencies.retrofitMoshi
    implementation rootProject.ext.dependencies.retrofitRxJava2
    implementation rootProject.ext.dependencies.stetho

    kapt rootProject.ext.dependencies.daggerCompiler
    kapt rootProject.ext.dependencies.daggerAndroidProcessor
}
